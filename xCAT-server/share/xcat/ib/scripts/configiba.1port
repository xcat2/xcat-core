#!/usr/bin/perl
# IBM(c) 2008 EPL license http://www.eclipse.org/legal/epl-v10.html

# Sample xCAT post script for configuring secondary adatper based on eth0 
# settings and some conventions.  This scripts works for both diskfull installs # and diskless boots.

# configiba.1port assume there is only port availabe per IB adapter.

use Socket;

#Check the platform
my $PLTFRM = `uname`;
chomp $PLTFRM;

my $HOST = `hostname -s`;
chomp $HOST;

my @nums = (0..1);
my @nics;
my $OS_name;
foreach my $num ( @nums ) {
    # Take primary node name, add -ib$num and then reverse resolve to get what ip should be
    my $nic = "ib$num";
    my $hostname;

    # Get hostname from system in case postscript environment is not ready
    if ( $HOST ) {
        $hostname = "$HOST-$nic";
    } else {
        $hostname = "$ENV{NODE}-$nic";
    }

    my $packed_ip = gethostbyname($hostname);
    if ( !$packed_ip ) { system("logger -t xcat 'configiba: cannot resolve $hostname.'"); next; }
    my $ip = inet_ntoa($packed_ip);
    #TODO: should contact xcatd on the service node to get the netmask and gateway from the networks table
    my $netmask = "255.255.255.0";
    my ($first, $second, $rest) = split(/\./, $ip);
    my $gateway = "$first.$second.255.254";

    if ( $PLTFRM eq "Linux" ) {
        # Issue openibd for Linux at boot time
        if ( -f '/etc/redhat-release' )
        {
            $OS_name = "redhat";
        }
        elsif ( -f '/etc/SuSE-release' )
        {
            $OS_name = "suse";
        }
        else
        {
            print "Unsupported to config IB on this OS!\n";
            exit;
        }
        if ( -f "/etc/sysctl.conf" )
        {
            runcmd("echo 'net.ipv4.conf.$nic.arp_filter=1' >> /etc/sysctl.conf");
            runcmd("echo 'net.ipv4.conf.$nic.arp_ignore=1' >> /etc/sysctl.conf");
        }
        if ( -f "/etc/infiniband/openib.conf" )
        {
            runcmd("/usr/bin/sed -i 's/SDP_LOAD=yes/SDP_LOAD=no/g' /etc/infiniband/openib.conf");
        }

        # Write the info to the ifcfg file
        my $dir;
        if ( $OS_name eq 'suse')
        {
            $dir = "/etc/sysconfig/network";
        }
        elsif ( $OS_name eq 'redhat' )
        {
            $dir = "/etc/sysconfig/network-scripts";
        }
        if ( !open(FILE, ">$dir/ifcfg-$nic") ) { system("logger -t xcat 'configiba: cannot open $dir/ifcfg-$nic.'"); exit 1; }
        print FILE "DEVICE=$nic\n";
        print FILE "BOOTPROTO=static\n";
        print FILE "IPADDR=$ip\n";
        print FILE "NETMASK=$netmask\n";
        print FILE "GATEWAY=$gateway\n";
        print FILE "ONBOOT=yes\n" if ( $OS_name eq 'redhat');
        print FILE "STARTMODE=auto\n" if ( $OS_name eq 'suse');
        close FILE;

        push @nics, $nic;
    } elsif ( $PLTFRM eq "AIX" ) {
    #Check whether the icm is available
        my $cmd = "lsdev -C | grep icm | grep Available";
        `$cmd`;
        if ($?) {
            my $iba_cmd = "mkdev -c management -s infiniband -t icm";
            runcmd($iba_cmd);
            if ($?) {
                my $iba_command = "mkdev -l icm";
                runcmd($iba_command);
                if ($?) { exit $?; }
            }
        }
        #Configure the IB interfaces.  Customize the port num.
        my $iba_num = $num;
        my $ib_adapter = "iba$iba_num";
        my $port = 1;
        my $mkib_cmd = "mkiba -a $ip -i $nic -A $ib_adapter -p $port -P -1 -S up -m $netmask";
        runcmd($mkib_cmd);
        system("logger -t xcat 'configiba: successfully configured $nic.'");
    }
}

if ( $PLTFRM eq "Linux" ) {
    if ( $OS_name eq 'suse')
    {
        runcmd("echo 'options ib_ehca nr_ports=1' >> /etc/modprobe.conf");
        runcmd("echo 'options ib_ehca lock_hcalls=0' >> /etc/modprobe.conf");
    }

    runcmd("sysctl -p");

    my $openibd_cmd = "/sbin/chkconfig --level 2345 openibd on";
    runcmd($openibd_cmd);
    my $openibd_start_cmd = "/sbin/service openibd restart";
    runcmd($openibd_start_cmd);

    sleep(20);
    foreach my $nic ( @nics ) {
        sleep(5);
        runcmd("ifup $nic");
        system("logger -t xcat 'configiba: successfully configured $nic.'");
    }
}


#Configure the ml0 interface
if ( $PLTFRM eq "AIX" ) {
    my $hostname;
    if ( $HOST ) {
        $hostname = "$HOST-ml0";
    } else {
        $hostname = "$ENV{NODE}-ml0";
    }
    my $packed_ip = gethostbyname($hostname);
    if ( !$packed_ip ) { system("logger -t xcat 'configiba: cannot resolve $hostname.'"); exit; }
    my $ip = inet_ntoa($packed_ip);
    my $netmask = "255.255.255.0";

    #Check whether the mlt0 is available
    my $cmd = "lsdev -C | grep mlt0 | grep Available 2>&1 >/dev/null";
    `$cmd`;
    if ( $? ) {
        system("logger -t xcat 'configiba: mlt0 interface is not available'");
        exit;
    }

    my $chdev_cmd = "chdev -l ml0 -a state=detach 2>&1";
    runcmd($chdev_cmd);

    $chdev_cmd = "chdev -l ml0 -a netaddr=$ip -a netmask=$netmask -a state=up 2>&1";
    runcmd($chdev_cmd);

    system("logger -t xcat 'configiba: configured ml0.'");
}


exit 0;

sub runcmd {
    my $cmd = shift @_;
    $cmd .= ' 2>&1';
    my @output = `$cmd`;
    my $rc = $? >> 8;
    if ( $rc ) {
        system("logger -t xcat 'configiba: command $cmd failed with rc $rc: " . join('',@output) . "'");
        return $rc;
    }
}

